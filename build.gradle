plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'goorm.back'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // postgresql DB
    runtimeOnly 'org.postgresql:postgresql'

    // RestDocs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // H2
    compileOnly 'com.h2database:h2'
    testImplementation 'com.h2database:h2'

    // Swagger
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0"

    // webclient
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // dynamo DB
    implementation platform('software.amazon.awssdk:bom:2.20.85')
    implementation 'software.amazon.awssdk:dynamodb-enhanced'

    // monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // zxing(QR)
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.google.zxing:javase:3.5.3'

    // aws Rekognition, S3
    implementation platform('software.amazon.awssdk:bom:2.20.112')
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:rekognition'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Message
    implementation 'net.nurigo:sdk:4.3.2'
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

// 제외할 디렉토리 및 파일 패턴을 공통 변수로 선언
ext.jacocoExcludes = [
        "**/generated/**", // Querydsl Qfile
        "**/dto/**",       // DTO 클래스
        "**/exception/**", // 커스텀 예외
        "**/config/**",    // 설정 파일
        "**/*Repository.*",  // 모든 repository 파일 제외
        "**/eventstore/**",
        "**/common/**"
]

tasks.named('jacocoTestReport').configure {
    dependsOn(tasks.named('test')) // 테스트 실행 후 리포트 생성
    reports {
        xml.required = false // SonarQube를 위한 XML 리포트 생성
        html.required = true // 사람이 읽기 쉬운 HTML 리포트 생성
        csv.required = false // CSV 리포트는 비활성화
    }
    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(dir) {
                    exclude jacocoExcludes // 리포트 생성에서 제외할 디렉토리와 파일 설정
                }
            })
    )

    finalizedBy tasks.named('jacocoTestCoverageVerification') // 리포트 생성 후 검증
}

tasks.jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = "BUNDLE" // 전체 모듈 수준에서 통합 커버리지 검증

            limit {
                counter = "INSTRUCTION" // 라인 커버리지 기준
                value = "COVEREDRATIO"
                minimum = 0.30
            }
            excludes = jacocoExcludes
        }
    }
}



asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    println("start bootJar")
    println("${asciidoctor.outputDir}")
    copy {
        from("${asciidoctor.outputDir}")
        into 'src/main/resources/static/docs'
    }
}